Shader "Hidden/Scene/FogOfWar"
{
	Properties
	{
		_Color ("Color", Color) = (1,1,1,1)
		_RecordTexture("Record Tex", 2D) = "black" {}
		_FOWVisionRadiusesTexture("Radius Tex", 2D) = "white" {}
		_FOWPositionsTexture("Position Tex", 2D) = "white" {}
		_ActualUnitsCount("_ActualUnitsCount", float) = 0
		_MaxUnits("_MaxUnits", float) = 1024
	}

	CGINCLUDE
		#include "UnityCG.cginc"
		uniform float _FogStrength;

		uniform sampler2D _CDepthTexture;
		
		sampler2D _FOWVisionRadiusesTexture;
        sampler2D _FOWPositionsTexture;
        sampler2D _RecordTexture;
        float _ActualUnitsCount;
        float _MaxUnits;
        float4 _StartPos;

        struct appdata
        {
        	float2 uv : TEXCOORD0;
        	float4 positionOS : POSITION;
        };
        
		struct v2f
		{
			float2 uv : TEXCOORD0;
			float3 positionWS : TEXCOORD1;
			float4 positionCS : SV_POSITION;
			float4 screenPos  : TEXCOORD2;
		};

		float4 _Color;

		v2f vert(appdata input)
		{
			v2f o = (v2f)0;
			o.uv = input.uv;
			o.positionCS = UnityObjectToClipPos(input.positionOS);
			o.positionWS = mul(unity_ObjectToWorld, input.positionOS).xyz;
			o.screenPos = ComputeScreenPos(o.positionCS);
			return o;
		}

		float4  GetWorldPos(v2f i, out float depth)
		{
			float4 screenPos = i.screenPos;
			float4 screenPosNorm = screenPos / screenPos.w;
			depth = SAMPLE_DEPTH_TEXTURE(_CDepthTexture, screenPosNorm.xy);

			#ifdef UNITY_REVERSED_Z
				depth = 1.0 - depth;
			#endif

			//transform screen position to world position; 
			float4 projectPos = float4(screenPosNorm.x, screenPosNorm.y, depth, 1.0);
			projectPos.xyz = projectPos.xyz * 2 - 1.0;
			float4 camPos = mul( unity_CameraInvProjection, projectPos );
			camPos.xyz /= camPos.w; 
			camPos.z *= -1; 
			camPos.w = 1.0f;
				
			float4 worldPos = mul( unity_CameraToWorld, camPos );

			return worldPos;
		}

		float4 frag(v2f input) : SV_TARGET0
		{
            float alpha = _FogStrength;
					
			float2 textureResolution = float2(_MaxUnits, 1);

			float depth = 0;
			float4  positionWS = GetWorldPos(input, depth);
			float3  pos = positionWS;
			pos.y = 0;

			float2 worldUV = (positionWS.xz - _StartPos.xy)/_StartPos.zw;
			half record = tex2D(_RecordTexture, worldUV).r * 0.5;
			if (record > 0.1) alpha = 1-record;

			bool fineded = false;
			UNITY_LOOP 
            for (int i = 0; i < _ActualUnitsCount; i++)
            {
                float2 unitPixelCenterPos = float2(i, 0) + 0.5; // 0.5 to sample center of pixel due we work in texel space
                float3 unitPosition = tex2D(_FOWPositionsTexture, unitPixelCenterPos / textureResolution).rgb * 1024; //_Positions[i].xyz;
                unitPosition.y = 0;
                float visionRadius = tex2D(_FOWVisionRadiusesTexture, unitPixelCenterPos / textureResolution).r * 512;
                float distanceToUnit = distance(unitPosition, pos);
                 
                UNITY_BRANCH
                if (distanceToUnit < visionRadius)
                {
                    float size = visionRadius - distanceToUnit;
                        
                    if (size < 1)
                        alpha = lerp(alpha, 0.01, size); // Insane Systems: previous alpha used because this sector can be already visible by other unit.
                    else 
                        alpha = 0;
                    fineded = true;
                }
            }

            alpha = clamp(alpha, 0, _FogStrength);

            float4  c = 0;
			c.a = alpha;
			
			return c; 
		}
	ENDCG

	SubShader
	{
		Tags {  "Queue"="Transparent"  "RenderType"="Opaque" }
		LOD 200
		ZTest Always
		ZWrite Off
		Blend SrcAlpha OneMinusSrcAlpha
		Cull Off

		Pass
		{
			CGPROGRAM

			#include "UnityCG.cginc"
			#pragma vertex vert
			#pragma fragment frag

			ENDCG
		}
	}
	FallBack "Diffuse"
}
